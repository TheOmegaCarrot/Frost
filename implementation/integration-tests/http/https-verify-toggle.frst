def host_env = getenv('HTTPS_VERIFY_TEST_HOST')
def host = if is_null(host_env): 'self-signed.badssl.com' else: host_env

def port_env = getenv('HTTPS_VERIFY_TEST_PORT')
def port_str = if is_null(port_env): '443' else: port_env

def port = to_int(port_str)

def skip_on_timeout = fn resp -> {
  if not resp.ok and resp.error.category == 'timeout':
    exit(77)
}

def verify_true_resp = http.request({
  endpoint: {
    host: host,
    tls: true,
    port: port,
    path: '/'
  },
  verify_tls: true,
  method: 'GET',
  timeout_ms: 30000
}).get()

skip_on_timeout(verify_true_resp)

assert(not verify_true_resp.ok, 'verify_tls true should fail on bad cert')
assert(verify_true_resp.error.category == 'TLS', 'verify_tls true should fail in TLS phase')

def verify_false_resp = http.request({
  endpoint: {
    host: host,
    tls: true,
    port: port,
    path: '/'
  },
  verify_tls: false,
  method: 'GET',
  timeout_ms: 30000
}).get()

skip_on_timeout(verify_false_resp)

assert(verify_false_resp.ok, 'verify_tls false should succeed on bad cert')
assert(verify_false_resp.response.code == 200, 'verify_tls false response code')
